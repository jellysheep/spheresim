
# Maintainer: Max Mertens <mail at sheepstyle dot comeze dot com>

pkgname=mingw-w64-spheresim-git
_gitname=spheresim
pkgver=latest
pkgrel=1
pkgdesc="Physical simulation of particle movements (server and clients). (mingw-w64)"
arch=('any')
url="http://sourceforge.net/projects/spheresim/"
license=('BSD 3-clause')
depends=('mingw-w64-crt' 'mingw-w64-qt5-base')
makedepends=('git' 'mingw-w64-gcc' 'mingw-w64-cmake')
options=('!strip' '!buildflags' 'staticlibs')
conflicts=('mingw-w64-spheresim')
provides=('mingw-w64-spheresim')

source=("${_gitname}::git+https://github.com/jellysheep/spheresim.git")

md5sums=('SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver()
{
    cd "${srcdir}/${_gitname}"
    cat VERSION
}

build()
{
    unset LDFLAGS
    CORE_COUNT="$(nproc)"

    cd "${srcdir}/${_gitname}"
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch}
        pushd build-${_arch}
        ${_arch}-cmake \
        -DBUILD_SHARED_LIBS:BOOL=ON \
        -DCMAKE_INSTALL_PREFIX:PATH="/usr/${_arch}" \
        .. -Wno-dev
        make -j"${CORE_COUNT}"
        popd
    done
}

package()
{
    for _arch in ${_architectures}; do
        cd "${srcdir}/${_gitname}/build-${_arch}"
        mkdir -p "${pkgdir}/usr/${_arch}/bin"
        mkdir -p "${pkgdir}/usr/${_arch}/bin/platforms/"
        make DESTDIR="${pkgdir}" install
        ln -s /usr/${_arch}/lib/qt5/plugins/platforms/qwindows.dll "${pkgdir}/usr/${_arch}/bin/platforms/"
    done
    mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}/"
    install "${srcdir}/${_gitname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
