
# Maintainer: Max Mertens <mail at sheepstyle dot comeze dot com> 

pkgname=mingw-w64-spheresim-git
_gitname=spheresim
pkgver=2.4.3
pkgrel=1
pkgdesc="Physical simulation of particle movements (server and clients). (mingw-w64)"
arch=('any')
url="http://sourceforge.net/projects/spheresim/"
license=('BSD 3-clause')
depends=('mingw-w64-crt' 'mingw-w64-qt5-base')
makedepends=('mingw-w64-gcc' 'mingw-w64-cmake' 'mingw-w64-eigen3')
options=('!strip' '!buildflags' 'staticlibs')
conflicts=('mingw-w64-spheresim')
provides=('mingw-w64-spheresim')

source=("${_gitname}::git+https://jellysheep@bitbucket.org/jellysheep/spheresim.git")

md5sums=('SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver()
{
	cd "${srcdir}/${_gitname}"
	cat VERSION
}

build()
{
	unset LDFLAGS
	CORE_COUNT="$(nproc)"

	cd "${srcdir}/${_gitname}"
	for _arch in ${_architectures}; do
		mkdir -p build-${_arch}
		pushd build-${_arch}
		${_arch}-cmake \
		-DBUILD_SHARED_LIBS:BOOL=ON \
		-DCMAKE_INSTALL_PREFIX:PATH="/usr/${_arch}" \
		..
		make -j"${CORE_COUNT}"
		popd
	done
}

package()
{
	for _arch in ${_architectures}; do
		cd "${srcdir}/${_gitname}/build-${_arch}"
		mkdir -p "${pkgdir}/usr/${_arch}/bin"
		mkdir -p "${pkgdir}/usr/${_arch}/bin/platforms/"
		make DESTDIR="${pkgdir}" install
		ln -s /usr/${_arch}/lib/qt5/plugins/platforms/qwindows.dll "${pkgdir}/usr/${_arch}/bin/platforms/"
		ln -s /usr/${_arch}/lib/{libstdc++-6.dll,libgomp-1.dll} "${pkgdir}/usr/${_arch}/bin/"
	done
	ln -s /usr/i686-w64-mingw32/lib/libgcc_s_sjlj-1.dll "${pkgdir}/usr/i686-w64-mingw32/bin/"
	mkdir -p "${pkgdir}/usr/share/licenses/${_gitname}/"
	install "${srcdir}/${_gitname}/LICENSE" "${pkgdir}/usr/share/licenses/${_gitname}/"
}
